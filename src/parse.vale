import stdlib.collections.list.*;
import stdlib.ifunction.*;

struct NotesCollector {
  nextId! int;
  ids List<int>;
  idByName HashMap<StrSlice, int, StrSliceHasher, StrSliceEquator>;
  nameById HashMap<int, StrSlice, IntHasher, IntEquator>;
  noteContentsById HashMap<int, Section, IntHasher, IntEquator>;
}
func NotesCollector(nextId int) NotesCollector {
  NotesCollector(
    nextId,
    List<int>(),
    HashMap<StrSlice, int, StrSliceHasher, StrSliceEquator>(
      StrSliceHasher(), StrSliceEquator()),
    HashMap<int, StrSlice, IntHasher, IntEquator>(IntHasher(), IntEquator()),
    HashMap<int, Section, IntHasher, IntEquator>(IntHasher(), IntEquator()))
}
func Slice(newColor StrSlice, notesCollector NotesCollector) Slice {
  [nextId, ids, idByName, nameById, noteContentsById] = notesCollector;
  return Slice(ids, newColor, noteContentsById);
}


func parse(filepath str) Page {
  s = readFileAsString(filepath);
  iter = ParseIter(s.slice());
  
  metadata = parseMetadata(&iter);

  notesCollector = NotesCollector(0);

  pageMembers = List<PageMember>();

  while iter.hasNext() {
    iter.skipNewlines();

    if iter.nextIs("<slice") {
      pageMembers.add(
          parseSlice(
              set notesCollector = NotesCollector(notesCollector.nextId),
              &iter));
    } else {
      section =
          parseSection(
              &notesCollector,
              &iter,
              &IFunction1<mut, &ParseIter, bool>((iter) => {
                return iter.hasNext() and not iter.nextIs("<slice") and not iter.nextIs("\n\n\n");
              }));
      pageMembers.add(section);
    }
  }

  pageMembers.add(
      Slice(
          "".slice(),
          set notesCollector = NotesCollector(notesCollector.nextId)));

  return Page(metadata, pageMembers);
}


func parseMetadata(input &ParseIter) Metadata {
  title = "".slice();
  subtitle = "".slice();
  author = "".slice();
  date = "".slice();
  realm = "".slice();
  path = "".slice();
  namespace = "".slice();
  layout = "".slice();
  seriesName = "".slice();
  sponsorLink = false;
  seriesEntries = List<Link>();
  cssFiles = List<StrSlice>();

  if input.skipIfNext("---\n") {
    while not input.skipIfNext("---\n") {
      attrName = input.getUntilExpected(":");
      input.skipWhitespace();

      if attrName == "title" {
        set title = input.getUntilExpected("\n");
      } else if attrName == "subtitle" {
        set subtitle = input.getUntilExpected("\n");
      } else if attrName == "author" {
        set author = input.getUntilExpected("\n");
      } else if attrName == "date" {
        set date = input.getUntilExpected("\n");
      } else if attrName == "realm" {
        set realm = input.getUntilExpected("\n");
      } else if attrName == "path" {
        set path = input.getUntilExpected("\n");
      } else if attrName == "namespace" {
        set namespace = input.getUntilExpected("\n");
      } else if attrName == "layout" {
        set layout = input.getUntilExpected("\n");
      } else if attrName == "sponsor" {
        input.getUntilExpected("\n");
        set sponsorLink = true;
      } else if attrName == "series-name" {
        set seriesName = input.getUntilExpected("\n");
      } else if attrName == "css" {
        cssFiles.add(input.getUntilExpected("\n"));
      } else if attrName == "series-entry" {
        seriesEntries.add(addLink(&input, true));
        input.skipExpect("\n");
      } else {
        panic("Unknown attribute: " + attrName);
      }
    }
  }

  if seriesEntries.len() > 0 {
    // A series link with an empty url represents the current page
    foundEmptyLink = false;
    foreach link in &seriesEntries {
      if link.text == "" and link.url == "" {
        vassert(not foundEmptyLink, "Already an empty series entry!");
        set foundEmptyLink = true;
      }
    }
    vassert(foundEmptyLink, "No empty series entry!");
  }

  return Metadata(title, subtitle, author, date, realm, path, seriesName, seriesEntries, layout, namespace, cssFiles, sponsorLink);
}


func getListItemPrefix(style str) str {
  if style == "bullet" { " * " }
  else if style == "ordered" { " 1. " }
  else if style == "blank" { " . " }
  else { panic("unknown list style") }
}


func skipListItemPrefix(iter &ParseIter) {
  if iter.skipIfNext(" * ") { }
  else if iter.skipIfNext(" 1. ") { }
  else if iter.skipIfNext(" . ") { }
  else { panic ("Expected list item prefix!"); }
}


func nextIsListItem(originalIter &ParseIter, style str) bool {
  lookaheadIter = copy(&originalIter);
  lookaheadIter.skipNewlines();
  while lookaheadIter.skipIfNext("   ") { }
  return lookaheadIter.nextIs(getListItemPrefix(style));
}


func parseList(
    notesCollector &NotesCollector,
    iter &ParseIter,
    continueF &IFunction1<mut, &ParseIter, bool>)
PolyList {
  style =
      if iter.nextIs(" 1. ") { "ordered" }
      else if iter.nextIs(" * ") { "bullet" }
      else if iter.nextIs(" . ") { "blank" }
      else { panic("unknown list style") };

  rootList = PolyList(style, List<ListMember>());
  // A stack of all the currently open lists.
  openLists = List<&PolyList>();
  openLists.add(&rootList);

  while continueF(iter) and iter.nextIsListItem(style) {
    iter.skipNewlines();
    thisListItemIndentation = 1;
    while iter.skipIfNext("   ") {
      set thisListItemIndentation = thisListItemIndentation + 1;
    }
    skipListItemPrefix(iter);

    while thisListItemIndentation > openLists.len() {
      newList = PolyList(style, List<ListMember>());
      newListBorrow = &newList;
      openLists.get(openLists.len() - 1).members.add(newList);
      openLists.add(newListBorrow);
    }
    while thisListItemIndentation < openLists.len() {
      openLists.remove(openLists.len() - 1);
    }

    lineContents =
        parseLine(
            &notesCollector,
            &iter,
            &IFunction1<mut, &ParseIter, bool>((iter) => {
              return continueF(iter) and not iter.nextIs("\n");
            }));
    openLists.get(openLists.len() - 1).members.add(ListItem(lineContents));
  }

  return rootList;
}


func parseQuote(
    notesCollector &NotesCollector,
    iter &ParseIter,
    continueF &IFunction1<mut, &ParseIter, bool>)
Quote {
  inlines = List<InlineNode>();

  while continueF(iter) and iter.nextIs("> ") {
    iter.skipExpect("> ");

    lineContents =
        parseInlines(
            &notesCollector,
            &iter,
            &IFunction1<mut, &ParseIter, bool>((iter) => {
              return continueF(iter) and not iter.nextIs("\n");
            }));
    while lineContents.inners.len() > 0 {
      inlines.add(lineContents.inners.remove(0));
    }
  }

  return Quote(Compound(inlines));
}

func parseCallout(
    notesCollector &NotesCollector,
    iter &ParseIter,
    continueF &IFunction1<mut, &ParseIter, bool>)
Callout {
  inlines = List<InlineNode>();

  while continueF(iter) and iter.nextIs("! ") {
    iter.skipExpect("! ");

    lineContents =
        parseInlines(
            &notesCollector,
            &iter,
            &IFunction1<mut, &ParseIter, bool>((iter) => {
              return continueF(iter) and not iter.nextIs("\n");
            }));
    while lineContents.inners.len() > 0 {
      inlines.add(lineContents.inners.remove(0));
    }
  }

  return Callout(Compound(inlines));
}


func parseSplit(
    notesCollector &NotesCollector,
    iter &ParseIter)
Split {
  iter.skipExpect("<<<<");
  iter.skipNewlines();

  columns = List<List<Section>>();

  running = true;
  while running {
    innerContinueF =
        IFunction1<mut, &ParseIter, bool>((iter) => {
          if not iter.hasNext() {
            panic("<<<< must end with \/\/\/\/");
          }
          return not iter.nextIs("\/\/\/\/") and not iter.nextIs(">>>>");
        });

    columnSections = List<Section>();

    while innerContinueF(iter) {
      columnSections.add(
          parseSection(
              &notesCollector,
              &iter,
              &IFunction1<mut, &ParseIter, bool>((iter) => {
                return innerContinueF(iter) and not iter.nextIs("\n\n\n");
              })));
      iter.skipNewlines();
    }

    columns.add(columnSections);

    set running = iter.skipIfNext("\/\/\/\/");
    iter.skipNewlines();
  }

  iter.skipNewlines();
  iter.skipExpect(">>>>");

  return Split(columns);
}


func nextIsNoteBegin(originalIter &ParseIter) bool {
  lookaheadIter = copy(&originalIter);
  if not lookaheadIter.skipIfNext("\n\n") {
    return false;
  }
  lookaheadIter.skipNewlines();
  return lookaheadIter.skipIfNext("#");
}


func parseSlice(
    notesCollector NotesCollector,
    iter &ParseIter)
Slice {
  iter.skipExpect("<slice");

  newColor = "".slice();
  if iter.skipIfNext(" new-color=\"") {
    set newColor = iter.getUntilExpected("\"");
  }
  if iter.skipIfNext(">") {
    iter.skipNewlines();

    continueF =
        IFunction1<mut, &ParseIter, bool>((iter) => {
          if not iter.hasNext() {
            panic("<slice> must end with </slice>");
          }
          return not iter.nextIs("</slice>");
        });

    while continueF(iter) {
      iter.skipNewlines();
      iter.skipExpect("#");
      noteId = iter.getUntilExpected(":");

      noteOutput =
          parseSection(
              &notesCollector,
              &iter,
              &IFunction1<mut, &ParseIter, bool>((iter) => {
                return continueF(iter) and not iter.nextIsNoteBegin();
              }));
      iter.skipNewlines();
      notesCollector.addSeparateNote(noteId, noteOutput);
    }

    iter.skipIfNext("</slice>");
  } else if (iter.skipIfNext("/>") or iter.skipIfNext(" />")) {
    // Do nothing
  } else {
    panic("<slice should end in > or />");
  }

  return Slice(newColor, notesCollector);
}


func parseInlineNode(
    notesCollector &NotesCollector,
    iter &ParseIter)
Opt<InlineNode> {
  if not iter.PreviousChar().slice().isWordChar() and iter.nextIs("*") {
    return Some<InlineNode>(parseBold(notesCollector, iter));
  } else if (not iter.PreviousChar().slice().isWordChar() and iter.nextIs("_")) {
    return Some<InlineNode>(parseItalic(notesCollector, iter));
  } else if (iter.nextIs("[#")) {
    return Some<InlineNode>(addInlineNote(notesCollector, iter));
  } else if (not iter.PreviousChar().slice().isWordChar() and iter.nextIs("[")) {
    return Some<InlineNode>(addLink(&iter, false));
  } else if (not iter.PreviousChar().slice().isWordChar() and iter.skipIfNext("`")) {
    code = iter.getUntilExpected("`");
    return Some<InlineNode>(InlineCode(code));
  } else {
    return None<InlineNode>();
  }
}

func parseInlines(
    notesCollector &NotesCollector,
    iter &ParseIter,
    continueF &IFunction1<mut, &ParseIter, bool>)
Compound {
  inners = List<InlineNode>();

  currentTextNode StrSlice = "".slice();

  running = true;
  while running and iter.hasNext() {
    if not continueF(iter) {
      set running = false;
    } else {
      maybeInner = parseInlineNode(notesCollector, iter);
      if not maybeInner.isEmpty() {
        if currentTextNode != "" {
          inners.add(Text(set currentTextNode = "".slice()));
        }
        inners.add((maybeInner).get());
      } else {
        (maybeInner).drop();

        iter.skipIfNext("\\");
        nextChar = iter.getNext();

        set currentTextNode = (currentTextNode + nextChar).slice();
      }
    }
  }
  if currentTextNode != "" {
    inners.add(Text(set currentTextNode = "".slice()));
  }

  return Compound(inners);
}

func parseBold(
    notesCollector &NotesCollector,
    iter &ParseIter)
Bold {
  iter.skipExpect("*");

  compound =
    parseInlines(
      &notesCollector,
      &iter,
      &IFunction1<mut, &ParseIter, bool>((iter) => {
        if not iter.hasNext() {
          panic("Must end bold!");
          return false;
        } else if (iter.nextIs("*")) {
          iter.skipExpect("*");
          return false;
        } else {
          return true;
        }
      }));

  return Bold(compound);
}

func parseItalic(
    notesCollector &NotesCollector,
    iter &ParseIter)
Italic {
  iter.skipExpect("_");

  compound =
    parseInlines(
      &notesCollector,
      &iter,
      &IFunction1<mut, &ParseIter, bool>((iter) => {
        if not iter.hasNext() {
          panic("Must end italic!");
          return false;
        } else if (iter.nextIs("_")) {
          iter.skipExpect("_");
          return false;
        } else {
          return true;
        }
      }));

  return Italic(compound);
}

func parseLine(
    notesCollector &NotesCollector,
    iter &ParseIter,
    continueF &IFunction1<mut, &ParseIter, bool>)
InlineNode {
  return parseInlines(
    &notesCollector,
    &iter,
    &IFunction1<mut, &ParseIter, bool>((iter) => {
      return continueF(iter) and iter.hasNext();
    }));
}



func parseTextUntil(
    iter &ParseIter,
    continueF &IFunction1<mut, &ParseIter, bool>)
StrSlice {
  code StrSlice = "".slice();

  running = true;
  while running and iter.hasNext() {
    if not continueF(iter) {
      set running = false;
    } else {
      set code = (code + iter.getNext()).slice();
    }
  }

  return code;
}


func parseCodeItem(notesCollector &NotesCollector, iter &ParseIter) CodeItem {
  // println("go");

  iter.skipExpect("```");

// println("ko");

  lang =
      parseTextUntil(
          &iter,
          &IFunction1<mut, &ParseIter, bool>((it) => {
            if not it.hasNext() {
               panic("``` must have matching \\n```!");
            }
            return not it.skipIfNext("\n");
          }));
  if lang.contains(":") {
    panic("Language has : in it!");
  }
  if lang.contains(" ") {
    panic("Language has space in it!");
  }
  if lang.contains("`") {
    panic("Language has ` in it!");
  }

// println("bo " + lang);

  // We register the chevron notes here and leave the code mostly untouched, because:
  // - If we slice up the code into a Compound with Text and NoteAnchor, then
  //   its harder later on to merge the text parts of the code back together to
  //   feed into the syntax highlighter (and hard to know where to put the
  //   anchors back in).
  // - If we syntax highlight the code now, it means we're making the decision
  //   now on how to display it, which belongs in the printing stage.
  // Though, we do replace the note name with a note id, to be consistent with how
  // NoteAnchor works.

  code =
      parseChevronNoteAnchors(
          &notesCollector,
          parseTextUntil(
              &iter,
              &IFunction1<mut, &ParseIter, bool>((it) => {
// println("bi");

                if not it.hasNext() {
// println("mi");

                  panic("``` must have matching \\n```!");
                }
// println("di");

                return not it.nextIs("\n```");
              })));
// println("vo");
  return CodeItem(lang, code.slice());
}

func parseCodeArgs(iter &ParseIter) List<StrSlice> {
  args = List<StrSlice>();

  // println("ante loop");

  running = true;
  while running {

    // println("moo");

    while iter.skipIfNext(" ") { }

    // println("boo");
    
    if iter.skipIfNext("\n") {
      // println("zoo");
    
      set running = false;
    } else if iter.skipIfNext("`") {
      // println("woo");
      x = iter.getUntilExpected("`");
      args.add(slice("`" + x + "`"));
    } else {
      // println("xoo");
      // Get until whitespace
      arg = parseTextUntil(
          &iter,
          &IFunction1<mut, &ParseIter, bool>((it) => {
            if not iter.hasNext() {
              panic("``` must have matching \\n```!");
            }
            return not iter.nextIs(" ") and not iter.nextIs("\n");
          }));
      // println("koo");
      args.add(arg);
    }
  }
  // println("post loop");

  return args;
}

func parseCodeSnippet(notesCollector &NotesCollector, iter &ParseIter) CodeSnippet {
  args = List<StrSlice>();
  items = List<CodeItem>();

    // println("before loop");
  running = true;
  while running {
    // println("in an iter");
    items.add(parseCodeItem(notesCollector, iter));

    vassert(iter.nextIs("\n```"), "Bad snippet end!");
    iter.skipExpect("\n");
    
    // println("parsed item");
    if iter.hasNext() and iter.skipIfNext("```:") {
      // Now we'll parse args that come after the language, like in:
      //   ```Vale
      //   func main() {
      //     a = A();
      //   }
      //   ```: vstl/list.vale `struct A{}`
      set args = parseCodeArgs(iter);
      set running = false;
    }
    if not iter.hasNext() or iter.skipIfNext("```\n") {
      // println("breaking");
      set running = false;
    }
    // println("end an iter");
  }

  return CodeSnippet(items, args);
}


// Looks through for any chevron notes like «mynote» and registers them with
// the notes collector.
func parseChevronNoteAnchors(notesCollector &NotesCollector, haystack StrSlice) str {
  result = "";

  iter = ParseIter(haystack);

  while iter.hasNext() {
    if iter.skipIfNext("«") {
      noteName = iter.getUntilExpected("»");
      id = addNamedNoteAnchor(notesCollector, noteName);
      set result = result + ("«" + id + "»");
    } else {
      set result = result + iter.getNext();
    }
  }

  return result;
}


func parseSection(
    notesCollector &NotesCollector,
    iter &ParseIter,
    continueF &IFunction1<mut, &ParseIter, bool>)
Section {
  sectionMembers = List<SectionMember>();

  while continueF(iter) {
    iter.skipNewlines();

    if iter.nextIs("```") {
      sectionMembers.add(parseCodeSnippet(notesCollector, iter));
    } else if (iter.nextIs("#")) {
      headerStrength = 0;
      while iter.skipIfNext("#") {
        set headerStrength = headerStrength + 1;
      }
      headerContents =
          parseInlines(
              &notesCollector,
              &iter,
              &IFunction1<mut, &ParseIter, bool>((it) => {
                return iter.hasNext() and not iter.nextIs("\n");
              }));
      sectionMembers.add(Header(headerStrength, headerContents));
    } else if (iter.nextIs("<<<<")) {
      sectionMembers.add(parseSplit(notesCollector, iter));
    } else if (iter.nextIs(" * ") or iter.nextIs(" 1. ") or iter.nextIs(" . ")) {
      sectionMembers.add(
          parseList(
              &notesCollector,
              &iter,
              &IFunction1<mut, &ParseIter, bool>((iter) => {
                return continueF(iter) and not iter.nextIs("\n\n");
              })));
    } else if (iter.nextIs("> ")) {
      sectionMembers.add(
          parseQuote(
              &notesCollector,
              &iter,
              &IFunction1<mut, &ParseIter, bool>((iter) => {
                return continueF(iter) and not iter.nextIs("\n\n");
              })));
    } else if (iter.nextIs("! ")) {
      sectionMembers.add(
          parseCallout(
              &notesCollector,
              &iter,
              &IFunction1<mut, &ParseIter, bool>((iter) => {
                return continueF(iter) and not iter.nextIs("\n\n");
              })));
    } else {
      inlineNode =
          parseLine(
              &notesCollector,
              &iter,
              &IFunction1<mut, &ParseIter, bool>((iter) => {
                return continueF(iter) and not iter.nextIs("\n\n");
              }));
      sectionMembers.add(Paragraph(inlineNode));
    }
  }

  return Section(sectionMembers);
}







func addLink(
    iter &ParseIter,
    allow_empty bool)
Link {
  iter.skipExpect("[");
  linkText = iter.getUntilExpected("]");
  iter.skipExpect("(");
  linkUrlAndAttrsStr = iter.getUntilExpected(")");
  if linkUrlAndAttrsStr.len() == 0 and allow_empty {
    return Link(linkText, "".slice(), List<StrSlice>());
  }
  vassert(linkUrlAndAttrsStr.len() > 0, "Bad link!");
  linkUrlAndAttrs = linkUrlAndAttrsStr.split(" ");
  vassert(linkUrlAndAttrs.len() > 0, "Bad link!");
  linkUrl = linkUrlAndAttrs.remove(0);
  classes = List<StrSlice>();
  foreach x in linkUrlAndAttrs {
    if x.startsWith(".") {
      classes.add(x.slice(1));
    } else {
      panic("Bad link attribute: {x}");
    }
  }

  return Link(linkText, linkUrl, classes);
}


func addInlineFullNote(
    notesCollector &NotesCollector,
    noteContents Section)
int {
  id = (set notesCollector.nextId = notesCollector.nextId + 1);
  notesCollector.ids.add(id);

  notesCollector.noteContentsById.add(id, noteContents);

  return id;
}

func addNamedNoteAnchor(
    notesCollector &NotesCollector,
    noteName StrSlice)
int {
  id = (set notesCollector.nextId = notesCollector.nextId + 1);
  notesCollector.ids.add(id);

  vassert(
      not notesCollector.idByName.ContainsKey(noteName),
      "Note " + noteName + " already exists!");

  notesCollector.idByName.add(noteName, id);
  notesCollector.nameById.add(id, noteName);

  return id;
}

func addInlineNote(
    notesCollector &NotesCollector,
    iter &ParseIter)
NoteAnchor {
  iter.skipExpect("[#");

  // If theres just space after the hash, then we're defining the note's text inline.
  if iter.skipIfNext(" ") {
    subNotes = NotesCollector(0);
    subNoteContents =
        parseInlines(
            &subNotes,
            &iter,
            &IFunction1<mut, &ParseIter, bool>((iter) => {
              if not iter.hasNext() {
                panic("Must end note!");
              }
              return not iter.skipIfNext("]");
            }));

    paragraph = Paragraph(subNoteContents);
    section = Section(List<SectionMember>());
    section.members.add(paragraph);

    return NoteAnchor(addInlineFullNote(notesCollector, section));
  } else {
    noteName =
        iter.parseTextUntil(
            &IFunction1<mut, &ParseIter, bool>((it) => {
              if not iter.hasNext() {
                panic("Must end note!");
              }
              return not iter.skipIfNext("]");
            }));
    return NoteAnchor(addNamedNoteAnchor(notesCollector, noteName));
  }
}

func addSeparateNote(
    self &NotesCollector,
    noteName StrSlice,
    note Section) {
  vassert(
      self.idByName.ContainsKey(noteName),
      "Note " + noteName + " doesn't exist!");
  id = self.idByName.get(noteName).get();


  vassert(
      not self.noteContentsById.ContainsKey(id),
      "Note " + noteName + " already has contents!");

  self.noteContentsById.add(id, note);
}

